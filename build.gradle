plugins {
    id 'java'
    id 'maven-publish'
}

group = 'albinowax'
version = '1.4.1'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.portswigger.burp.extender:burp-extender-api:2.3'
    implementation 'net.portswigger.burp.extensions:montoya-api:2023.10.3'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

archivesBaseName = 'albinowaxUtils-all'

tasks.register('fatJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with(tasks.getByName("jar")) // Fixed method to reference the 'jar' task
}

publishing {
    publications {
        gpr(MavenPublication) {
            from(components.java)
            artifact(tasks.named('fatJar').get()) // Use 'tasks.named' to access the fatJar task
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/portswigger-katie/albinowaxUtils")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.named('generateMetadataFileForGprPublication').configure {
    dependsOn tasks.named('fatJar')
}

